buildscript {
    ext {
        springBootVersion = '1.5.7.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
    id "com.moowork.node" version "1.2.0"
    id "org.sonarqube" version "2.5"
    id "com.jfrog.artifactory" version "4.4.12"
}

apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'java'
apply plugin: "jacoco"
apply plugin: 'org.springframework.boot'
apply plugin: 'maven-publish'

apply from: "$rootDir/gradle/versioning.gradle"
apply from: "$rootDir/gradle/docker.gradle"

node {
    download = true
    version = '6.11.3'
    workDir = file("${project.buildDir}/node")
    nodeModulesDir = file("${project.projectDir}")
}

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile('org.springframework.boot:spring-boot-starter-data-rest')
    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    compile('org.springframework.data:spring-data-rest-hal-browser')

    compileOnly('org.projectlombok:lombok')

    runtime('org.springframework.boot:spring-boot-devtools')
    runtime('org.hsqldb:hsqldb')

    testCompile('org.springframework.boot:spring-boot-starter-test')
}

task copyWebApp(type: Copy) {
    from('dist') {
        include '**/*.js'
    }
    from('src/main/webapp') {
        include '**/*.css'
        include '**/*.html'
    }
    into 'build/resources/main/static'

    includeEmptyDirs = false
}

npm_run_build.inputs.dir new File(projectDir, "src/main/webapp")
npm_run_build.outputs.dir new File(projectDir, "dist")

npm_run_build.dependsOn npm_install

copyWebApp.outputs.dir new File(projectDir, "dist")
copyWebApp.dependsOn npm_run_build

jar.dependsOn copyWebApp
build.dependsOn createDockerfile, npm_install, copyWebApp

// =============================
//  JUnit
// =============================

test {
    group = 'test'
    description = 'Runs all unit tests for this project (fast)'
    useJUnit {
        includeCategories 'com.toedter.test.category.UnitTest'
    }
    jacoco {
        destinationFile = file("$buildDir/jacoco/jacoco.exec")
    }
}

task integrationTest(type: Test) {
    group = 'test'
    description = 'Runs all integration tests for this project (slow)'
    useJUnit {
        includeCategories 'com.toedter.test.category.IntegrationTest'
    }
    reports.junitXml.destination = file("$buildDir/test-results/test/")
    jacoco {
        destinationFile = file("$buildDir/jacoco/jacoco-it.exec")
    }
    // set build directory for Jenkins test harness, JENKINS-26331
    systemProperty 'buildDirectory', project.buildDir.absolutePath
}

// =============================
//  JaCoCo
// =============================

jacoco {
    toolVersion = "0.7.9"
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

task jacocoMergeReports(type: JacocoMerge) {
    destinationFile = file("${project.buildDir}/jacoco/coverage.exec")
    def unitSource = new File("${project.buildDir}/jacoco/jacoco.exec")
    def integrationSource = new File("${project.buildDir}/jacoco/jacoco-i.exec")
    if (unitSource.exists() && integrationSource.exists()) {
        executionData = files(unitSource, integrationSource)
    } else if (unitSource.exists()) {
        executionData = files(unitSource)
    } else if (integrationSource.exists()) {
        executionData = files(integrationSource)
    }
}

// =============================
//  SonarQube
// =============================

sonarqube {
    // see https://docs.sonarqube.org/display/SCAN/Analyzing+with+SonarQube+Scanner+for+Gradle
    properties {
        property "sonar.projectName", "${project.name}"
        property "sonar.branch", branchFromScm()
        property "sonar.junit.reportsPath", "${project.buildDir}/test-results/test/"
        property "sonar.jacoco.reportPaths", "${project.buildDir}/jacoco/coverage.exec"
    }
}
tasks.sonarqube.dependsOn jacocoMergeReports //, junitMergeReports

def branchFromScm() {
    return "git rev-parse --abbrev-ref HEAD".execute().text.trim()
}

// =============================
//  Artifactory
// =============================

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

artifactory {
    publish {
        contextUrl = 'http://artifactory.pipeline:8081/artifactory'
        repository {
            repoKey = 'libs-release-local'
            username = System.getenv('ARTIFACTORY_LOGIN')
            password = System.getenv('ARTIFACTORY_PASSWORD')
        }
        defaults {
            publications('mavenJava')
            publishArtifacts = true
            publishPom = true
        }
    }
}

